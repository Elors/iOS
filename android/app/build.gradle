apply plugin: 'com.android.application'


android {
    signingConfigs {
        release {
            try {
                storeFile file(KEYSTORE_FILE_PATH)
                storePassword KEYSTORE_PASSWORD
                keyAlias "malalaoshi"
                keyPassword KEY_PASSWORD
            }
            catch (ex) {
                throw new InvalidUserDataException("You should define KEYSTORE_FILE_PATH, KEYSTORE_PASSWORD and KEY_PASSWORD in gradle.properties.")
            }
        }
    }
    compileSdkVersion 23
    buildToolsVersion '23.0.2'

    // necessary for volley.jar, as sdk 23 has removed org.apache.http.
    useLibrary 'org.apache.http.legacy'

    defaultConfig {
        applicationId "com.malalaoshi.android"
        minSdkVersion 15
        targetSdkVersion 23
        versionCode 1
        def buildID = System.getenv("BUILD_NUMBER") + "_" + System.getenv("JOB_NAME")
        versionName "1.0" + (buildID == null ? "" : "." + buildID)
        buildConfigField("String", "API_HOST", "\"" + LOCAL_SERVER + "\"")
    }
    buildTypes {
        debug {
            applicationIdSuffix ".debug"
            buildConfigField "boolean", "LOG_DEBUG", "true"
            versionNameSuffix "-debug"
            minifyEnabled false
            zipAlignEnabled false
        }

        release {
            buildConfigField "boolean", "LOG_DEBUG", "false"
            minifyEnabled true
            zipAlignEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
    }

    lintOptions {
        abortOnError true
    }

    productFlavors {
        teacher {
            applicationId "com.malalaoshi.android.teacher"
        }
        parent {
            applicationId "com.malalaoshi.android.parent"
        }
    }

    sourceSets {
        main {
            jniLibs.srcDirs = ['libs']
        }
    }
}

dependencies {
    compile fileTree(include: ['*.jar'], dir: 'libs')
    testCompile 'junit:junit:4.12'
    compile 'com.android.support:design:23.1.1'
    compile 'com.jakewharton:butterknife:7.0.1'
    compile 'com.fasterxml.jackson.core:jackson-core:2.6.+'
    compile 'com.android.support:appcompat-v7:23.1.1'
    compile 'com.android.support:support-v4:23.1.1'
    compile 'com.android.support:recyclerview-v7:23.1.1'
    compile 'com.android.support:cardview-v7:23.1.1'
    compile 'com.pnikosis:materialish-progress:1.7'
    compile 'com.nineoldandroids:library:2.4.0'
    compile 'cn.kuake:bga-refreshlayout:0.11.31'
    compile 'com.google.code.gson:gson:2.2.4'
}

