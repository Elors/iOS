apply plugin: 'com.android.application'

def releaseTime(){
    return new Date().format("yyyy-MM-dd", TimeZone.getTimeZone("Asia/Shanghai"))
}

android {
    signingConfigs {
        release {
            try {
                storeFile file(KEYSTORE_FILE_PATH)
                storePassword KEYSTORE_PASSWORD
                keyAlias "malalaoshi"
                keyPassword KEY_PASSWORD
            }
            catch (ex) {
                throw new InvalidUserDataException("You should define KEYSTORE_FILE_PATH, KEYSTORE_PASSWORD and KEY_PASSWORD in gradle.properties.")
            }
        }
    }
    compileSdkVersion 22
    buildToolsVersion "22.0.1"
    defaultConfig {
        applicationId "com.malalaoshi.android"
        minSdkVersion 15
        targetSdkVersion 22
        versionCode 1
        versionName "1.0"
        buildConfigField("String", "API_HOST", "\""+LOCAL_SERVER+"\"")
    }
    buildTypes {
        debug{
            applicationIdSuffix ".debug"
            buildConfigField "boolean", "LOG_DEBUG", "true"
            versionNameSuffix "-debug"
            minifyEnabled false
            zipAlignEnabled false
        }

        release {
            buildConfigField "boolean", "LOG_DEBUG", "false"
            minifyEnabled true
            zipAlignEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
    }

    applicationVariants.all{ variant ->
        variant.outputs.each{ output ->
            def outputFile = output.outputFile
            if(outputFile != null && outputFile.name.endsWith('.apk')){
                def fileName = outputFile.name.substring(0, outputFile.name.length() - 4) + '_' + releaseTime() + '.apk'
                output.outputFile = new File(outputFile.parent, fileName)
            }
        }
    }

    lintOptions {
        abortOnError false
    }

    productFlavors {
        teacher {
            applicationId "com.malalaoshi.android.teacher"
        }
        parent {
            applicationId "com.malalaoshi.android.parent"
        }
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    testCompile 'junit:junit:4.12'
    compile 'com.android.support:appcompat-v7:22.2.1'
    compile 'com.android.support:design:22.2.1'
    compile 'com.android.support:support-v4:22.2.1'
    compile 'com.fasterxml.jackson.core:jackson-core:2.6.+'
    compile project(':volley')
}
